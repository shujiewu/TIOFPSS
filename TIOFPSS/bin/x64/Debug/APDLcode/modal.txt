!/CLEAR
/INPUT,'para_gear122','txt','D:\abc\APDL0320\',, 0   !路径
/CWD,filepath
/FILNAME,friction,0
/PREP7 
!本程序采用毫米单位制进行编写 (MCP5之后加入齿侧间隙)
!内毂参数定义
pi=3.14159265358979
r=0.5*m*z             !分度圆半径
rb=r*cos(angle1)       !基圆半径
ra1=0.5*(z*m+2*ha1)     !内毂齿顶圆半径
rf1=0.5*(z*m-2*c1)  !内毂齿根圆半径  
!摩擦片参数定义
ra2=0.5*(z*m-2*ha2)     !摩擦片齿顶圆半径
rf2=0.5*(z*m+2*c2)  !摩擦片齿根圆半径  

angle2=180.0/z        !镜像旋转角   
angle3=(pi/2.0/z+tan(angle1)-angle1)*180.0/pi   !初始的偏转角(其值不能改变）  
angle4=(pi/2.0/z+tan(angle1)-angle1)*180.0/pi   !对称偏转角  
 
ET,1,SOLID185     !定义单元类型
ET,2,SHELL181 
sect,1,shell,,  
secdata, 0.1,1,0.0,3
secoffset,MID   
seccontrol,,,, , , ,
 

MPTEMP,,,,,,,,     !定义内毂材料参数
MPTEMP,1,0  
MPDATA,EX,1,,ex1
MPDATA,PRXY,1,,prxy1
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,DENS,1,,dens1

MPTEMP,,,,,,,,     !定义芯板材料参数
MPTEMP,1,0  
MPDATA,EX,2,,ex2
MPDATA,PRXY,2,,prxy2
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,DENS,2,,dens2

MPTEMP,,,,,,,,     !定义摩擦层材料参数
MPTEMP,1,0  
MPDATA,EX,3,,ex3
MPDATA,PRXY,3,,prxy3
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,DENS,3,,dens3

/prep7
csys,4               !获得内毂旋转角度                 
wprot,-angle3,0,0      
K,1,0,0                
 
*do,t,0,1,0.01               
*SET,x,rb*(cos(t)+t*sin(t))  
*SET,y,rb*(sin(t)-t*cos(t))   
k,,x,y,0               
*enddo   
 
flst,3,101,3             
*do,t,2,102
fitem,3,t    
*enddo   
bsplin,,p51x           
 
KDELE,3,101,1          
NUMCMP,KP              

LGEN,2,1, , ,0,0,10, ,0   
CSKP,11,0,2,4,3,1,1,

csys,1 
LGEN,2,1, , ,0,gap1/r*180/pi,0, ,0
LGEN,2,1, , ,0,-gap2/r*180/pi,0, ,0

LDELE,1,2 ,1 ,1
NUMCMP,KP   
NUMCMP,line 
circle,1,ra2 
LSBL,1,6 
LDELE,3,5,1 ,1
circle,1,ra2 
LSBL,2,5
LDELE,1, , ,1
LDELE,3,4,1 ,1
LDELE,6,7,1 ,1
NUMCMP,KP   
NUMCMP,line 
csys,1  
gky=gap1/r*180/pi+gap2/r*180/pi
LDELE,1,2,1,1

!建立内毂模型 
WPCSYS,-1,0 
csys,4  
wprot,-angle3,0,0   
K,1,0,0 
*do,t,0,1,0.01  
*SET,x,rb*(cos(t)+t*sin(t)) 
*SET,y,rb*(sin(t)-t*cos(t)) 
k,,x,y,0
*enddo  
flst,3,101,3
*do,t,2,102 
fitem,3,t   
*enddo  
bsplin,,p51x
KDELE,3,101,1   
NUMCMP,KP   
LGEN,2,1, , ,0,0,10, ,0 
CSKP,11,0,2,4,3,1,1,
csys,1 
LGEN,2,1, , ,0,gap1/r*180/pi,0, ,0 
ldele,1,2,1 
kdele,2,5,1 
NUMCMP,KP   
NUMCMP,line 
CSYS,4  
wprot,angle4,0,0
LSYMM,Y,1, , , ,0,0 
circle,1,ra1
LSBL,1,6
LSBL,2,3
ldele,4 
ldele,5 
ldele,6 
ldele,8 
kdele,6,9,1 
kdele,3 
kdele,5 
NUMCMP,line 
NUMCMP,KP   
circle,1,rf1
LSBL,1,3
LSBL,2,6
ldele,4 
ldele,5 
ldele,1 
ldele,7 
kdele,7,9,1 
kdele,2 
kdele,3 
NUMCMP,line 
NUMCMP,KP   
larc,2,3,1,ra1  
LGEN,2,1, 3,1 , , , , ,0
ldele,1,3,1,1   
NUMCMP,line 
NUMCMP,KP   
circle,1,rf1
circle,1,rd1
circle,1,rf1-1.2
LSTR,       5,       1  
LSTR,       1,       3  
LSBL,       4,      16  
LSBL,       7,      17  
LDELE,4, , ,1   
LDELE,18, , ,1  
CSYS,1  
LSEL,S,LOC,Y,20,50  
WPCSYS,-1,0 
wpro,,90.000000,
wprot,,,180/z   
LSBW,all
ALLSEL,ALL  
CSYS,1  
LSEL,S,LOC,Y,-20,-50
WPCSYS,-1,0 
wpro,,90.000000,
wprot,,,-180/z  
LSBW,all
ALLSEL,ALL  
CSYS,1  
LSEL,S,LOC,Y,30,330 
LDELE,all, , ,1 
ALLSEL,ALL  
NUMCMP,line 
NUMCMP,KP   
LCOMB,6, 7,0
LCOMB,8, 9,0
lfillt,2,4,rff1  
lfillt,1,5,rff1  
LSTR,1,9
LSTR,9,6
LSTR,11,12  
LSTR,12,10  
CSYS,1  
LSEL,U,LOC,X,rd1-1,rf1-2
AL,ALL  
ALLSEL,ALL  
LPLOT   
LSEL,U,LOC,X,rf1-1,ra1+1
AL,ALL  
ALLSEL,ALL  
CSYS,4  
WPCSYS,-1,0 
VOFFST,1,-b1
VOFFST,2,b1  
NUMCMP,line
NUMCMP,KP  
NUMCMP,VOLU
    
CSYS,4  
WPCSYS,-1,0 
wprot,angle2,0,0
VSYMM,Y,1,2,1 , ,0,0
*do,t,1,z-2,1   
wprot,2*angle2,0,0  
VSYMM,Y,3+(t-1)*2,4+(t-1)*2,1 , ,0,0
*enddo  
CSYS,1  
ALLSEL,ALL
NUMCMP,line 
NUMCMP,KP   
NUMCMP,AREA 
NUMCMP,VOLU
ALLSEL,ALL
ALLSEL,BELOW,VOLU 
VGLUE,ALL
NUMCMP,line 
NUMCMP,KP   
NUMCMP,AREA 
NUMCMP,VOLU
ALLSEL,ALL
ALLSEL,BELOW,VOLU 
*GET,VMN1,VOLU,,NUM,MIN 
*GET,VMX1,VOLU,,NUM,Max 

!建立摩擦片
CSYS,4  
WPCSYS,-1,0 
VSEL,U, , ,ALL  
ALLSEL,BELOW,VOLU   
K, ,,,, 
*GET,kzx,kp,,NUM,MIN
KSEL,U, , , kzx 
WPCSYS,-1,0 
wprot,-angle3,0,0   
*do,t,0,1,0.01  
*SET,x,rb*(cos(t)+t*sin(t)) 
*SET,y,rb*(sin(t)-t*cos(t)) 
k,,x,y,0
*enddo  
*GET,kpnub1,kp,,NUM,MIN 
*GET,kpnub2,kp,,NUM,Max 
flst,3,101,3
*do,t,kpnub1,kpnub2 
fitem,3,t   
*enddo  
bsplin,,p51x
KDELE,kpnub1+1,kpnub2-1,1   
NUMCMP,KP   
*GET,lnub,line,,NUM,max 
CSYS,1 
LGEN,2,lnub, , ,0,-gap2/r*180/pi,0, ,0 
ldele,lnub  
kdele,kpnub1,kpnub1+1,1 
NUMCMP,KP   
NUMCMP,line 
CSYS,4  
wprot,angle4,0,0
LSYMM,Y,lnub, , , ,0,0  
KSEL,A, , , kzx 
circle,kzx,ra2  
LSBL,lnub,lnub+5
LSBL,lnub+1,lnub+2   
ldele,lnub  
ldele,lnub+6
ldele,lnub+3
ldele,lnub+4
kdele,kpnub1+5,kpnub1+7,1   
kdele,kpnub1+2  
kdele,kpnub1
NUMCMP,line 
NUMCMP,KP   
circle,kzx,rf2  
LSBL,lnub,lnub+2
LSBL,lnub+1,lnub+5   
ldele,lnub  
ldele,lnub+6
ldele,lnub+3
ldele,lnub+4
ldele,1 
ldele,7 
kdele,kpnub1+5,kpnub1+7,1   
kdele,kpnub1+1  
kdele,kpnub1
NUMCMP,line 
NUMCMP,KP   
larc,kpnub1+2,kpnub1+3,kzx,rf2  
LGEN,2,lnub,lnub+2,1 , , , , ,0 
ldele,lnub,lnub+2,1,1   
NUMCMP,line 
NUMCMP,KP   
wprot,angle2,0,0
LSYMM,Y,lnub,lnub+2,1 , ,0,0
circle,kzx,rdc  
circle,kzx,rd2  
CSYS,1  
LSEL,S,LOC,X,rf2
LSEL,R,LOC,Y,0,0.1  
WPCSYS,-1,0 
wpro,,90.000000,
LSBW,lnub+2     !出错LSBW,all              
ALLSEL,ALL  
*GET,VMN1,VOLU,,NUM,MIN 
*GET,VMX1,VOLU,,NUM,Max 
CSYS,1  
LSEL,S,LOC,X,rf2
LSEL,A,LOC,X,rdc
LSEL,A,LOC,X,rdc+1.2
LSEL,A,LOC,X,rd2
LSEL,R,LOC,Y,angle4,60
WPCSYS,-1,0 
wprot,2*angle2,0,0  
wpro,,90.000000,
LSBW,lnub+5     !出错LSBW,all
LSBW,lnub+6
LSBW,lnub+10
ALLSEL,ALL  
NUMCMP,line 
NUMCMP,KP   
LSEL,S,,,lnub,lnub+23,1 
CSYS,1  
LSEL,U,LOC,Y,0,2*angle2 
LDELE,all, , ,1 
NUMCMP,line 
NUMCMP,KP   
LSEL,S,,,lnub,lnub+6,1  
larc,kpnub1,kpnub1+2,kzx,ra2
lfillt,lnub,lnub+4,rff2     !倒角
lfillt,lnub+1,lnub+5,rff2
LSTR,kpnub1+7,kpnub1+8  
LSTR,kpnub1+8,kpnub1+9  
LSTR,kpnub1+5,kpnub1+4  
LSTR,kpnub1+4,kpnub1+6  
CSYS,1  
LSEL,R,LOC,X,ra2,rdc
AL,ALL    !已定义的线生成面  
ALLSEL,ALL  !选择所有实体：点线面体
LSEL,s,LOC,X,rdc,rd2
AL,ALL     
ALLSEL,ALL  
CSYS,1 

ASEL,s,LOC,X,rdc,rd2
*GET,AC2,AREA,,NUM,MIN  
VOFFST,AC2,-(bx+bc) 
*if,bc,ne,0,then
VOFFST,AC2,bc
*endif

ALLSEL,ALL
ASEL,s,LOC,X,rf2-1,rd2
*GET,AC1,AREA,,NUM,MIN 
ALLSEL,ALL
CSYS,4  
WPCSYS,-1,0 
VOFFST,AC2-1,bx   
ALLSEL,ALL  
CSYS,1  
VSEL,s,LOC,X,ra2,rd2
VSEL,R,VOLU, ,VMX1+1,VMX1+3    
ALLSEL,BELOW,VOLU   
wpoff,,,bx  
VSBW,ALL        
VGLUE,ALL   
VGEN, ,ALL, , , , ,BC+0.5, , ,1 
NUMCMP,line 
NUMCMP,KP   
NUMCMP,AREA 
NUMCMP,VOLU 
*GET,VMN2,VOLU,,NUM,MIN 
*GET,VMX2,VOLU,,NUM,Max 

!网格划分 
CSYS,4  
WPCSYS,-1,0 
wprot,2*angle2,0,0  
VSYMM,Y,VMN2,VMX2,1 , ,0,0  
*do,t,1,z-2,1   
wprot,angle2,0,0 
VSYMM,Y,VMN2,VMX2,1 , ,0,0 
*enddo  
CSYS,1  
ALLSEL,ALL
NUMCMP,line 
NUMCMP,KP   
NUMCMP,AREA 
NUMCMP,VOLU
ALLSEL,ALL
VSEL,U, , ,1 ,VMX1,1
ALLSEL,BELOW,VOLU 
VGLUE,ALL
NUMCMP,line 
NUMCMP,KP   
NUMCMP,AREA 
NUMCMP,VOLU

ALLSEL,ALL             
VSEL,S, , ,1 ,VMX1,1
ALLSEL,BELOW,VOLU
VGEN, ,1,VMX1,1, ,gky, , , ,1   !调整接触 
ALLSEL,ALL   
ALLSEL,BELOW,VOLU
ALLSEL,ALL            
VSEL,S, , ,1 ,VMX1,1
ALLSEL,BELOW,VOLU
CSYS,4 
VGEN, ,1,VMX1,1, , ,BC+0.5-(B1-BX)/2, , ,1   
CSYS,1 

*dim,VN,,n3,2

*if,n3,gt,0,then  !产生内毂齿面随机数
NNN=z/n3

*do,t,1,n3,1
NX=rand(NNN*(t-1)+1,NNN*t)
NN=NINT(NX)
*SET,VN(t,1,1) , NN
*enddo

*endif

*if,bc,ne,0,then   !计算摩擦片对应的齿面

*do,t,1,n3,1

*if,VN(t,1,1),eq,1,then
*SET,VN(t,1,1) ,1
*else
*SET,VN(t,1,1) ,z+VN(t,1,1)
*endif


*if,VN(t,1,1),eq,z+2,then
*SET,VN(t,2,1) , 5*z+2
*elseif,VN(t,1,1),eq,1,then
*SET,VN(t,2,1) , 2*z+3
*elseif,VN(t,1,1),eq,z+3,then
*SET,VN(t,2,1) , 5*z+3
*else
*SET,VN(t,2,1) ,VN(t,1,1)+4*z
*endif

*enddo

*else

*do,t,1,n3,1

*if,VN(t,1,1),eq,1,then
*SET,VN(t,1,1) ,1
*else
*SET,VN(t,1,1) ,z+VN(t,1,1)
*endif

*if,VN(t,1,1),eq,z+2,then
*SET,VN(t,2,1) , 3*z+3
*elseif,VN(t,1,1),eq,1,then
*SET,VN(t,2,1) , 2*z+2
*elseif,VN(t,1,1),eq,z+3,then
*SET,VN(t,2,1) , 3*z+2
*else
*SET,VN(t,2,1) ,VN(t,1,1)+2*z
*endif

*enddo

*endif

ALLSEL,ALL
ALLSEL,BELOW,VOLU
VSEL,S, , ,VN(1,1,1),   !选择齿面体
VSEL,a, , ,VN(1,2,1),   
*do,t,2,n3,1
VSEL,a, , ,VN(t,1,1),
VSEL,a, , ,VN(t,2,1),
*enddo
ALLSEL,BELOW,VOLU 
VGLUE,ALL
NUMCMP,line 
NUMCMP,KP   
NUMCMP,AREA 
NUMCMP,VOLU


*GET,VMX3,VOLU,,NUM,Max
ALLSEL,ALL          !网格划分尺寸设定   
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU   
CSYS,1  
LSEL,R,LOC,X,rf1-1,ra1+1
LESIZE,ALL,0.5, , , ,1, , ,1,   
ALLSEL,ALL          
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1 ,VMX3,1 
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,X,rf1-1.2
LESIZE,ALL,1, , , ,1, , ,1, 
ALLSEL,ALL             
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1,VMX3,1  
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,X,0,rd1+1
LESIZE,ALL,3, , , ,1, , ,1, 
ALLSEL,ALL           
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,X,rd1+1,rf1-1.3  
LESIZE,ALL,2, , , ,1, , ,1, 
ALLSEL,ALL            
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1 ,VMX3,1  
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,Z,BC+0.5-(B1-BX)/2+0.001,BC+0.5-(B1-BX)/2+b1-0.001 
LESIZE,ALL,1.2, , , ,1, , ,1,     
 
ALLSEL,ALL          !网格划分尺寸设定   
VSEL,U, , ,1 ,VMX1-n3,1
VSEL,U, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU
CSYS,1  
LSEL,R,LOC,X,ra2-1,rdc-0.1  
LESIZE,ALL,0.5, , , ,1, , ,1,   
ALLSEL,ALL  
VSEL,U, , ,1 ,VMX1-n3,1
VSEL,U, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU   
CSYS,1  
LSEL,R,LOC,X,rdc
LESIZE,ALL,0.7, , , ,1, , ,1,   
ALLSEL,ALL  
VSEL,U, , ,1 ,VMX1-n3,1
VSEL,U, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU   
CSYS,1  
LSEL,R,LOC,X,rd2
LESIZE,ALL,2, , , ,1, , ,1, 
ALLSEL,ALL  
VSEL,U, , ,1 ,VMX1-n3,1
VSEL,U, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,X,rdc+1,rd2-1
LESIZE,ALL,2, , , ,1, , ,1, 
ALLSEL,ALL  
VSEL,U, , ,1 ,VMX1-n3,1
VSEL,U, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU   
CSYS,1  
LSEL,R,LOC,Z,bc+0.6,bc+bx+0.4   
LESIZE,ALL,1.2, , , ,1, , ,1, 

!VSEL,R,LOC,Z,bc+0.6,bc+bx+0.4

ALLSEL,ALL          !内毂网络  
ALLSEL,BELOW,VOLU
TYPE,   1   
MAT,       1
*do,t,1,z-1-n3,1 
VSWEEP,Z+1+t
*enddo
*if,VN(1,1,1),eq,1,then
VSWEEP,1+z
VSWEEP,1
*else
VSWEEP,1
*endif 
*do,t,1,z,1 
VSWEEP,1+t
*enddo 
*if,VN(1,1,1),eq,1,then
VCLEAR,1
VSWEEP,1
*else
VCLEAR,2
VSWEEP,2
*endif 

TYPE,   1       !摩擦片齿部网格
MAT,       2
*if,bc,ne,0,then

*if,VN(1,1,1),eq,1,then
VSWEEP,z*5+1-n3
*else
VSWEEP,Z*2+3-n3
*endif

*do,t,1,z-1-n3,1    
VSWEEP,z*5+1+t-n3
*enddo 
*do,t,1,n3,1 
VSWEEP,VMX3-2*n3+t
*enddo

VSWEEP,z*2+1-n3      !摩擦片基体网络
VSWEEP,z*2+5-n3
VSWEEP,z*2+6-n3

*do,t,1,z-3,1 
VSWEEP,z*2+8+t*3-n3
*enddo

*else

*if,VN(1,1,1),eq,1,then    !摩擦片齿部网络
VSWEEP,z*3+1-n3
*else
VSWEEP,Z*2+2-n3
*endif


*do,t,1,z-1,1 
VSWEEP,Z*3+1+t-n3
*enddo 

VSWEEP,Z*2+1-n3     !摩擦片基体网络
*do,t,1,z-1,1 
VSWEEP,Z*2+2+t-n3
*enddo
*if,VN(1,1,1),eq,1,then    
VSWEEP,Z*2+2-n3
*endif

*endif

TYPE,   1   
MAT,       3

*if,VN(1,1,1),eq,1,then
VSWEEP,Z*2+3-n3
*endif

*if,bc,ne,0,then
VSWEEP,z*2+2-n3
VSWEEP,z*2+7-n3
VSWEEP,z*2+8-n3
*do,t,1,z-3,1 
VSWEEP,z*2+9+t*3-n3
*enddo
VSWEEP,z*2+4-n3
VSWEEP,z*2+9-n3
VSWEEP,z*2+10-n3
*do,t,1,z-3,1 
VSWEEP,z*2+10+t*3-n3
*enddo
*endif

ALLSEL,ALL          !网格划分尺寸设定   
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU   
CSYS,1  
LSEL,R,LOC,X,rf1-1,ra1+1
LESIZE,ALL,0.5, , , ,1, , ,1,   
ALLSEL,ALL          !网格划分尺寸设定   
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1 ,VMX3,1 
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,X,rf1-1.2
LESIZE,ALL,1, , , ,1, , ,1, 
ALLSEL,ALL          !网格划分尺寸设定   
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1,VMX3,1  
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,X,0,rd1+1
LESIZE,ALL,3, , , ,1, , ,1, 
ALLSEL,ALL          !网格划分尺寸设定   
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1 ,VMX3,1
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,X,rd1+1,rf1-1.3  
LESIZE,ALL,2, , , ,1, , ,1, 
ALLSEL,ALL          !网格划分尺寸设定   
VSEL,S, , ,1 ,VMX1-n3,1
VSEL,A, , ,VMX3-n3+1 ,VMX3,1  
ALLSEL,BELOW,VOLU   
LSEL,R,LOC,Z,BC+0.5-(B1-BX)/2+0.001,BC+0.5-(B1-BX)/2+b1-0.001 
LESIZE,ALL,1.2, , , ,1, , ,1, 

ALLSEL,ALL          !网格划分尺寸设定  
!VSEL,S, , ,VMX3-n3+1,VMX3,1 
ALLSEL,BELOW,VOLU
TYPE,   1   
MAT,       1
MSHAPE,1,3D 
MSHKEY,0
*do,t,1,n3,1 
VMESH,VMX3-n3+t
*enddo  

ALLSEL,ALL         !创建内毂约束节点集合
ALLSEL,BELOW,VOLU
NSEL,S,LOC,x,rd1
CM,FORCE,NODE 
CSYS,1
CMSEL,S,FORCE     !摩擦片约束的施加
D,ALL, , , , , ,UX,UZ, , , , 
ALLSEL,ALL
SAVE
FINISH  

!求解
/SOL 
ANTYPE,MODAL 
MODOPT,LANB,dom,0,0, ,OFF   
MXPAND,dom, , ,YES 
OUTPR,BASIC
OUTRES,ALL,ALL
ALLSEL
SOLVE 
FINISH 
 
/RGB,INDEX,100,100,100, 0   
/RGB,INDEX, 80, 80, 80,13   
/RGB,INDEX, 60, 60, 60,14   
/RGB,INDEX, 0, 0, 0,15  
/REPLOT 
FINISH 

/POST1
/VIEW,1,,,1 
/ANG,1  
/REP,FAST 
*do,i,1,dom
/AUTO,1 
/REP,FAST
set,1,i
/DSCALE,ALL,1.0 
/EFACET,1
/GRAPHICS,POWER 
/show,jpeg,, 
PLNSOL, U,SUM, 0,1.0      !位移显示
/show,close 
/RENAME,friction000.JPG,,,modal_u_%i%.jpeg
*enddo
finish

/POST1
/VIEW,1,,,1 
/ANG,1  
/REP,FAST 
*do,i,1,dom
/AUTO,1 
/REP,FAST
set,1,i
/DSCALE,ALL,1.0 
/EFACET,1
/GRAPHICS,POWER 
/show,jpeg,, 
PLNSOL, S,EQV, 0,1.0     !等效应力显示
/show,close 
/RENAME,friction000.JPG,,,modal_s_%i%.jpeg
*enddo
finish





/post1                  
*get,nodenum,node,,num,max                  !读取节点数        
*set,modenum,dom                               !指定模态的阶数 
*cfopen,modal,csv,,loc                    !打开文件          
*Do,i,1,modenum,1                       
*Get,modefq,mode,i,freq 
*Vwrite,i,modefq
(F10.0,F20.11) 
*Enddo 
*cfclos
finish
save
